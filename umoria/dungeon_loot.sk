options:
	color: <##f7ffc5>
	color2: <##B4C788>
	prefix: &8[<##B4C788>&lUMORIA&8]&7
	permission: &7Insufficient Permissions
	admin: &8[<##B4C788>&lADMIN&8]&r
	world: world

on right click:
	world of player is "dungeon"
	event-block is trapped chest
	generateChest(event-block, player)

function generateChest(b: block, p: player):
	expPlayer({_p}, 1)
	set {_dir} to facing of {_b}
	if {_dir} is north:
		set block at location of {_b} to north facing chest
	else if {_dir} is west:
		set block at location of {_b} to west facing chest
	else if {_dir} is east:
		set block at location of {_b} to east facing chest
	else if {_dir} is south:
		set block at location of {_b} to south facing chest
	open inventory of {_b} to {_p}
	loop 29 times:
		set {_n} to random integer between 1 and 10000
		if {_n} is less than 2500:
			if {_n} is less than 50:
				set {_item} to random element out of {mobloot::rare::*}
				set {_qty} to 1
			else if {_n} is less than 400:
				set {_item} to random element out of {mobloot::uncommon::*}
				set {_qty} to 1
			else:
				set {_item} to random element out of {mobloot::common::*}
				set {_qty} to random integer between 1 and 4
			set slot loop-number of inventory of {_b} to {_qty} of {_item}
		else if {_n} is less than 2700:
			set {_value} to random integer between 5 and 20
			set {_id} to random integer between 1000 and 9999
			set {_arrow} to random integer between 1 and 100
			if {_arrow} is less than 90:
				set {_arrow} to arrow
				set {_name} to " &8┃ &7Type: Arrow"
			else if {_arrow} is less than 92:
				set {_arrow} to tipped arrow of fire resistance
				set {_name} to " &8┃ &7Type: Solar Arrow"
			else if {_arrow} is less than 94:
				set {_arrow} to tipped arrow of swiftness
				set {_name} to " &8┃ &7Type: Lunar Arrow"
			else if {_arrow} is less than 96:
				set {_arrow} to tipped arrow of poison
				set {_name} to " &8┃ &7Type: Terrestrial Arrow"
			else if {_arrow} is less than 98:
				set {_arrow} to tipped arrow of weakness
				set {_name} to " &8┃ &7Type: Midnight Arrow"
			else if {_arrow} is less than 100:
				set {_arrow} to spectral arrow
				set {_name} to " &8┃ &7Type: Thunder Bolt"
			set slot loop-number of inventory of {_b} to {_value} of {_arrow} named "{@color}&lARROWS" with lore "%{_name}%" and " &8┃ &7Quantity: {@color}%{_value}%" and " &8╹ &7##%{_id}%" with all flags hidden
		else if {_n} is less than 3500:
			set {_value} to random integer between 2 and 15
			set {_id} to random integer between 1000 and 9999
			set slot loop-number of inventory of {_b} to gold nugget named "{@color}&lCOINS" with lore " &8┃ &7Quantity: {@color}%{_value}%" and " &8╹ &7##%{_id}%" 
		else if {_n} is less than 3700:
			set {_silver} to random integer between 1 and 100
			if {_silver} is less than 10:
				set {_silver} to {polishedsilver}
				set {_count} to 1
			else if {_silver} is less than 40:
				set {_silver} to {weatheredsilver}
				set {_count} to random integer between 1 and 2
			else:
				set {_silver} to {tarnishedsilver}
				set {_count} to random integer between 1 and 3
			set slot loop-number of inventory of {_b} to {_count} of {_silver}

on inventory click:
	name of event-inventory is "Chest"
	world of player is "dungeon"
	if event-item is gold nugget named "{@color}&lCOINS":
		cancel event
		set {_lore::*} to uncolored lore of event-item 
		set {_value::*} to {_lore::1} parsed as " ┃ Quantity: %number%"
		remove event-item from event-inventory
		coins("add", player, {_value::1})
	else if name of event-item contains "arrow":
		cancel event
		if item amount of event-item is not set:
			set {_n} to 1
		else:
			set {_n} to item amount of event-item
		if 1 of event-item is arrow:
			set {_arrow} to "arrow"
		else if "%event-item%" contains "fire resistance":
			set {_arrow} to "solar"
		else if "%event-item%" contains "swiftness":
			set {_arrow} to "lunar"
		else if "%event-item%" contains "poison":
			set {_arrow} to "terrestrial"
		else if "%event-item%" contains "weakness":
			set {_arrow} to "midnight"
		else if 1 of event-item is spectral arrow:
			set {_arrow} to "thunder"
		set {_slot} to index of event-slot
		if {quiver.%player%::%{_arrow}%} is 100:
			stop
		else if ({quiver.%player%::%{_arrow}%} + {_n}) is greater than 100:
			set {_extra} to ({quiver.%player%::%{_arrow}%} + {_n}) - 100
			set {quiver.%player%::%{_arrow}%} to 100
			set {_origItem} to 1 of event-item
			updateQuiverInventory(player)
			set slot {_slot} of event-inventory to brown candle named "{@color}&lQUIVER" with lore " &8┃ &7Added to quiver"
			wait 1 second
			set slot {_slot} of event-inventory to {_extra} of {_origItem}
		else:
			set {quiver.%player%::%{_arrow}%} to {quiver.%player%::%{_arrow}%} + {_n}
			updateQuiverInventory(player)
			set slot {_slot} of event-inventory to brown candle named "{@color}&lQuiver" with lore " &8┃ &7Added to quiver"
			wait 1 second
			set slot {_slot} of event-inventory to air
	else if event-item is brown candle:
		cancel event