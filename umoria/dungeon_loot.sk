options:
	color: <##f7ffc5>
	color2: <##B4C788>
	prefix: &8[<##B4C788>&lUMORIA&8]&7
	permission: &7Insufficient Permissions
	admin: &8[<##B4C788>&lADMIN&8]&r
	world: world

on right click:
	world of player is "dungeon"
	event-block is trapped chest
	generateChest(event-block, player)

function generateChest(b: block, p: player):
	set {_dir} to facing of {_b}
	if {_dir} is north:
		set block at location of {_b} to north facing chest
	else if {_dir} is west:
		set block at location of {_b} to west facing chest
	else if {_dir} is east:
		set block at location of {_b} to east facing chest
	else if {_dir} is south:
		set block at location of {_b} to south facing chest
	open inventory of {_b} to {_p}
	loop 29 times:
		set {_n} to random integer between 1 and 10000
		if {_n} is less than 2500:
			if {_n} is less than 50:
				set {_item} to random element out of {mobloot::rare::*}
				set {_qty} to 1
			else if {_n} is less than 400:
				set {_item} to random element out of {mobloot::uncommon::*}
				set {_qty} to 1
			else:
				set {_item} to random element out of {mobloot::common::*}
				set {_qty} to random integer between 1 and 4
			set slot loop-number of inventory of {_b} to {_qty} of {_item}
		else if {_n} is less than 4000:
			set {_value} to random integer between 2 and 15
			set {_id} to random integer between 1000 and 9999
			set slot loop-number of inventory of {_b} to gold nugget named "{@color}&lCOINS" with lore " &8┃ &7Quantity: {@color}%{_value}%" and " &8╹ &7##%{_id}%" 
		else if {_n} is less than 4500:
			set {_silver} to random integer between 1 and 100
			if {_silver} is less than 10:
				set {_silver} to {polishedsilver}
				set {_count} to 1
			else if {_silver} is less than 40:
				set {_silver} to {weatheredsilver}
				set {_count} to random integer between 1 and 2
			else:
				set {_silver} to {tarnishedsilver}
				set {_count} to random integer between 1 and 3
			set slot loop-number of inventory of {_b} to {_count} of {_silver}

on inventory click:
	name of event-inventory is "Chest"
	world of player is "dungeon"
	event-item is gold nugget named "{@color}&lCOINS"
	cancel event
	set {_lore::*} to uncolored lore of event-item 
	set {_value::*} to {_lore::1} parsed as " ┃ Quantity: %number%"
	remove event-item from event-inventory
	coins("add", player, {_value::1})
	# send "{@prefix} &7Recieved %{_value::1}% coins from the chest. "
	set {action::%player%} to "+%{_value::1}% coins"
	set {actionCoins::%player%} to {_value::1}
	wait 2 seconds
	{actionCoins::%player%} is {_value::1}
	delete {action::%player%}