function copyDungeon(loc: location, name: text, yt: integer, zt: integer, xt: integer):
	remove 1 from y-coordinate of {_loc}
	set {_n} to {_zt} / 2
	remove floor({_n}) from z-coordinate of {_loc}
	broadcast "%floor({_n})%"
	set {_x} to x-location of {_loc}
	set {_y} to y-location of {_loc}
	set {_z} to z-location of {_loc}
	loop {_yt} times:
		loop {_zt} times:
			loop {_xt} times:
				set {_offsetX} to x-location of {_loc} - {_x}
				set {_offsetY} to y-location of {_loc} - {_y}
				set {_offsetZ} to z-location of {_loc} - {_z}
				set {d_%{_name}%::%{_offsetX}%.%{_offsetY}%.%{_offsetZ}%} to block at {_loc}
				set {d_%{_name}%::dimensions} to "%{_yt}%, %{_zt}%, %{_xt}%"
				add 1 to x-coordinate of {_loc}
			set x-coordinate of {_loc} to {_x}
			add 1 to z-coordinate of {_loc}
		set z-coordinate of {_loc} to {_z}
		add 1 to y-coordinate of {_loc}
	set y-coordinate of {_loc} to {_y}

function pasteDungeon(loc: location, name: text):
	set {_dimensions::*} to {d_%{_name}%::dimensions} split by ", "
	remove 1 from y-coordinate of {_loc}
	set {_n} to ({_dimensions::2} parsed as integer) / 2
	remove floor({_n}) from z-coordinate of {_loc}
	set {_x} to x-location of {_loc}
	set {_z} to z-location of {_loc}
	set {_y} to y-location of {_loc}
	loop ({_dimensions::1} parsed as integer) times:
		loop ({_dimensions::2} parsed as integer) times:
			loop ({_dimensions::3} parsed as integer) times:
				set {_offsetX} to loop-number-3
				remove 1 from {_offsetX}
				set {_offsetZ} to loop-number-2
				remove 1 from {_offsetZ}
				set {_offsetY} to loop-number-1
				remove 1 from {_offsetY}
				set block at {_loc} to {d_%{_name}%::%{_offsetX}%.%{_offsetY}%.%{_offsetZ}%}
				add 1 to x-coordinate of {_loc}
			set x-coordinate of {_loc} to {_x}
			add 1 to z-coordinate of {_loc}
		set z-coordinate of {_loc} to {_z}
		add 1 to y-coordinate of {_loc}
	set y-coordinate of {_loc} to {_y}

on tab complete of "/dungeon":
	set tab completions for position 1 to "clearStart", "listStart", "listRoom", "copy", "delete", "save", "load", "remove", and "paste"
	set tab completions for position 2 to {d_roomList::*}

command /dungeon [<text>] [<text>] [<integer>] [<integer>] [<integer>]:
	trigger:
		if player is not op:
			send "no permission"
			stop
		if arg-1 is "clearStart":
			delete {dungeonStart::*}
			broadcast "deleted all dungeon start"
			stop
		if arg-1 is "listStart":
			loop {dungeonStart::*}:
				broadcast "%loop-index%, %loop-value%, %block at loop-value%"
		if arg-1 is "listRoom":
			loop {d_roomList::*}:
				broadcast "&f%loop-value%&7: %{d_%loop-value%::dimensions}%"
		if arg-1 is "copy" or "save":
			if arg-2 is not set:
				broadcast "/dungeon <copy> <name>"
				stop
			else:
				copyDungeon({dungeonStart.pos}, arg-2, arg-3, arg-4, arg-5)
				broadcast "copied new dungeon to %arg-2%"
				if {d_roomList::*} does not contain arg-2:
					add arg-2 to {d_roomList::*}
				stop
		if arg-1 is "paste" or "load":
			if arg-2 is not set:
				broadcast "/dungeon <paste> <name>"
				stop
			else:
				pasteDungeon({dungeonStart.pos}, arg-2)
				broadcast "pasted %arg-2%"
				stop
		if arg-1 is "delete" or "remove":
			if arg-2 is not set:
				broadcast "/dungeon <delete> <name>"
				stop
			else:
				broadcast "deleted %arg-2%"
				if {d_roomList::*} contains arg-2:
					remove arg-2 from {d_roomList::*}
				stop
		arg-1 is not set
		loop {dungeonStart::*}:
			if block at loop-value is emerald block:
				broadcast "&aOpen: &7%loop-index%, %loop-value%"
				teleport player to location above loop-value
				stop
			else if block at loop-value is redstone block:
				broadcast "&cActive: &7%loop-index%, %loop-value%"
			else if block at loop-value is coal block:
				broadcast "&8Disabled: &7%loop-index%, %loop-value%"

on left click with blaze rod:
	cancel event
	player is op
	if event-block is obsidian or lapis lazuli block or diamond block:
		set {dungeonStart.pos} to location of event-block
		broadcast "&3Selection: &7%location of event-block%"
		stop
	else if event-block is redstone block or emerald block or coal block:
		if {dungeonStart::%location of event-block%} is set:
			delete {dungeonStart::%location of event-block%}
			send "removed %location of event-block%"
		else:
			set {dungeonStart::%location of event-block%} to location of event-block
			set yaw of {dungeonStart::%location of event-block%} to -90
			set pitch of {dungeonStart::%location of event-block%} to 0
			send "added %location of event-block%"

on walking on lapis lazuli block:
	world of player is "dungeon"
	pasteDungeon(location of event-block, "corridor")
	set event-block to stone bricks

on walking on diamond block:
	world of player is "dungeon"
	pasteDungeon(location of event-block, "defaultEmpty")
	set event-block to stone bricks